plugins {
    id "com.google.protobuf" version "0.8.18"
    id 'java'
    id 'application'

//    these are for debugging
    id 'org.barfuin.gradle.taskinfo' version '1.0.5'
}
apply plugin: 'java'
apply plugin: 'com.google.protobuf'

group 'com.flipkart'
version '1.0-SNAPSHOT'

def grpc_version = "1.13.1"
def protobufVersion = '3.3.1'
def protocVersion = "3.5.1-1"

repositories {
    maven {
        url "https://maven-central.storage-download.googleapis.com/maven2/"
    }
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    implementation "io.grpc:grpc-all:${grpc_version}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}


protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}"
        }
    }

    generatedFilesBaseDir = "$projectDir/src/main/generated"

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
            task.plugins {
                grpc {}
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'src/generated/main/java'
        }
    }
}


test {
    useJUnitPlatform()
}

startScripts.enabled = false

task startAJukebox(type: CreateStartScripts) {
    mainClass = 'com.flipkart.grpcsampleclients.JukeboxClient'
    applicationName = 'start-a-grpc-jukebox-client'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(startAJukebox)
    fileMode = 0755
}
